{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "aebaa1bd-7669-4c7e-b1c0-ea511a1dd515",
   "metadata": {},
   "outputs": [],
   "source": [
    "# The following code implements the modern McEliece cryptosystem in Sage. Note that SageMath 10 is required.\n",
    "\n",
    "m = 7\n",
    "t = 5\n",
    "k.<a> = FiniteField(2^m)\n",
    "R.<z> = PolynomialRing(k)\n",
    "\n",
    "\n",
    "def chunks(lst, n):\n",
    "# Yield successive n-sized chunks from lst.\n",
    "    for i in range(0, len(lst), n):\n",
    "        yield lst[i:i + n]\n",
    "        \n",
    "def translate_to_field(msg,m):\n",
    "# take a message msg of m bits and translate it to a field element of F_2^m\n",
    "    count = 0\n",
    "    x = k.zero()\n",
    "    for j in range(m):\n",
    "        if msg[m-j-1]:\n",
    "            x = x + a^j\n",
    "    return x\n",
    "\n",
    "def translate_to_field_vector(v,m):\n",
    "# take a message vector v of m*t bits and translate it to a vector of t field elements of F_2^m\n",
    "    result = []\n",
    "    for j in chunks(v,m):\n",
    "        result.append(translate_to_field(j,m))\n",
    "    return result\n",
    "\n",
    "def translate_from_field(x,m):\n",
    "# take a field element x in 2^m and translate it to a message of m bits\n",
    "    if (x == 0):\n",
    "        return [0]*m\n",
    "    if (x == 1):\n",
    "        result = [0]*m\n",
    "        result[0] = 1\n",
    "        return result\n",
    "    return x.list()\n",
    "\n",
    "def translate_from_field_vector(x,m):\n",
    "    vec = [translate_from_field(x[j],m) for j in range(len(x))]\n",
    "    return matrix(vec).transpose()\n",
    "\n",
    "def translate_from_field_matrix(M,m):\n",
    "    # if matrix is t x n, the result is mt x n\n",
    "    result = translate_from_field_vector(M[0,:].list(),m)\n",
    "    for j in range(M.nrows()-1):\n",
    "        result = result.stack(translate_from_field_vector(M[j+1,:].list(),m))\n",
    "    return result\n",
    "\n",
    "def generate_error(n,w):\n",
    "# generates a (somewhat random) n-bit vector of Hamming weight exactly w\n",
    "#    p = w/n\n",
    "#    result = [0]*n\n",
    "#    count = 0\n",
    "#    while count == 0:\n",
    "#        for j in range(n):\n",
    "#            r = random()\n",
    "#            if (r < p):\n",
    "#                result[j] = 1\n",
    "#                count = count + 1\n",
    "#            if (count == w):\n",
    "#                break\n",
    "#   return result\n",
    "    if (w > n):\n",
    "        return vector(matrix.ones(1,n))\n",
    "    result = [0]*(n-w)\n",
    "    result.extend([1]*w)\n",
    "    shuffle(result)\n",
    "    return result\n",
    "           \n",
    "def trunc_poly(p,n):\n",
    "    q = z;\n",
    "    for j in range(0,n):\n",
    "        p = (p-p(0)) // q\n",
    "    return p\n",
    "\n",
    "def parity_check_entry(g,i,j, list):\n",
    "    # need 0<i<=t, 0<j<=n\n",
    "    # outputs the (i,j)-entry of the parity check matrix of the Goppa code corresponding to g\n",
    "    return (trunc_poly(g,t-i)(list[j]))/g(list[j])\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "00f35801-bddd-4576-83e0-ff107bf1268f",
   "metadata": {},
   "outputs": [],
   "source": [
    "class McEliece:\n",
    "\n",
    "    def randomgoppa(self):\n",
    "    # creates a random irreducible Goppa polynomial of degree t over F_2^m\n",
    "        g = R.zero()\n",
    "        while not (g.is_irreducible()):\n",
    "            g = R.random_element(degree = t);\n",
    "        return g;\n",
    "        \n",
    "    def keygen(self):\n",
    "        g = self.randomgoppa()\n",
    "        # generate a random permutation of all field elements (n = 2^m) and write it over F_2\n",
    "        list_field = list(k)\n",
    "        perm = Permutations(2^m).random_element()\n",
    "        list_field_perm = [list_field[i-1] for i in perm]\n",
    "        parity = matrix(k,t,2^m, lambda x,y: parity_check_entry(g,x,y,list_field_perm))\n",
    "        parity = translate_from_field_matrix(parity,m)\n",
    "        parity = parity.rref() \n",
    "        # want parity to be of the form (I_mt | Q) where Q is mt x (n-mt)\n",
    "        # this doesn't necessarily happen immediately\n",
    "        # swap columns, swapping the variables in the permutation as well\n",
    "        new_parity = copy(parity)\n",
    "        for row in range(m*t):\n",
    "            if (parity[row,row] == 0):\n",
    "                vec = vector(parity[row,:].list())\n",
    "                for col in range(2^m):\n",
    "                    if (vec[col] == 1):\n",
    "                        new_parity.swap_columns(row, col)\n",
    "                        list_field_perm[row], list_field_perm[col] = list_field_perm[col], list_field_perm[row]\n",
    "                        break\n",
    "        new_parity = new_parity.rref()\n",
    "        Q = new_parity[:,m*t:2^m] # generator and parity check matrix can be deduced from this and don't need to be stored\n",
    "        return (Q, g, list_field_perm)\n",
    "    \n",
    "    def __init__(self):\n",
    "        (self.public_key, self.private_poly, self.private_perm) = self.keygen()\n",
    "\n",
    "# note that a generates the finite field F_2^m, z is the indeterminate of the polynomial ring over this field, and x is the indeterminate of the quotient of this ring over the Goppa poly\n",
    "\n",
    "    def encrypt(self, msg):\n",
    "        # msg must have n-m*t bits\n",
    "        G = self.public_key.stack(matrix.identity(2^m-m*t)) # generator matrix, n x (n-mt)\n",
    "        error = vector(generate_error(2^m,t))\n",
    "        msg = vector(GF(2), msg)\n",
    "        # G*msg is in the Goppa code. Now add the random error and output to get the codeword\n",
    "        return (G*msg) + error\n",
    "    \n",
    "    def EEA_patterson(self, r,g):\n",
    "    # applies standard EEA, but terminates as soon as the degrees fulfill the conditions of Patterson's algorithm\n",
    "        r_fixed = r\n",
    "        g_fixed = g\n",
    "        cap = g.degree()\n",
    "        u = R.zero()\n",
    "        u1 = R.one()\n",
    "        v = R.one()\n",
    "        v1 = R.zero()\n",
    "        while r.degree() > floor(cap/2) or u1.degree() > floor((cap-1)/2):\n",
    "            (quo,rem) = r.quo_rem(g)\n",
    "            r = g; g = rem\n",
    "            u2 = u1; u1 = u; u = u2-quo*u\n",
    "            v2 = v1; v1 = v; v = v2-quo*v\n",
    "            # r = u1*r_fixed + v1*g_fixed\n",
    "        return (r,u1)\n",
    "\n",
    "    def errorlocator(self, c):\n",
    "        Q.<x> = R.quotient(self.private_poly(z))     \n",
    "        # Apply Patterson's algorithm to derive the error locator polynomial over F_2^m\n",
    "        # c = Gm + e   \n",
    "        # compute syndrome s\n",
    "        s = Q.zero()\n",
    "        for j in range(2^m):\n",
    "            s += c[j] / (x - self.private_perm[j])\n",
    "        if (s == Q.zero()):\n",
    "            return Q.zero()\n",
    "        T = 1/s\n",
    "        if (T == x):\n",
    "            return x   \n",
    "        # now find square root r of T(z)+z. Q is a field with 2^(mt) elements, so Frobenius is the identity\n",
    "        r = (T+x)^(2^(m*t-1))\n",
    "        r = R(r.list())\n",
    "        (A,B) = self.EEA_patterson(r,self.private_poly)\n",
    "        return (A^2+z*B^2)\n",
    "\n",
    "    def error_from_errorlocator(self, sigma):\n",
    "        # finds the roots of the error locator polynomial and turns it into an error vector, applying the secret permutation on the way\n",
    "        error = [0]*2^m\n",
    "        for j in range(2^m):\n",
    "            if (sigma(self.private_perm[j]) == R.zero()):\n",
    "                error[j] = 1\n",
    "        return error\n",
    "    \n",
    "    def decrypt(self, c):\n",
    "        sigma = self.errorlocator(c)\n",
    "        error = vector(self.error_from_errorlocator(sigma))\n",
    "        c = c-error # = Gm\n",
    "        # now can read off msg as the last n-mt digits\n",
    "        return c[(m*t):2^m]\n",
    "\n",
    "\n",
    "        "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "8f71a69f-01d2-4878-87e4-42949590a43e",
   "metadata": {
    "scrolled": true
   },
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "This is the message\n",
      "[1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 1, 0, 1, 1, 0, 1, 0, 0, 0, 1, 0, 1, 1, 1, 1, 0, 1, 1, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 0, 0, 1, 0, 1, 1, 1, 0, 0, 0, 1, 0, 0, 1, 0, 1, 0, 1, 1, 0, 0, 1, 0, 1, 0, 1, 1, 1, 0, 1, 1, 1, 0, 1, 1, 0, 0, 1, 1, 0, 1, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1]\n",
      "This is the codeword:\n",
      "(0, 1, 0, 1, 0, 1, 1, 0, 0, 0, 1, 1, 0, 0, 1, 1, 1, 1, 0, 1, 0, 0, 0, 0, 1, 0, 0, 1, 1, 1, 0, 1, 0, 1, 1, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 1, 0, 1, 1, 0, 0, 0, 0, 0, 1, 0, 1, 1, 1, 1, 0, 1, 1, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 0, 0, 1, 0, 1, 1, 1, 0, 0, 0, 0, 0, 0, 0, 0, 1, 0, 0, 1, 0, 0, 1, 0, 1, 0, 1, 1, 1, 0, 1, 1, 1, 0, 1, 1, 0, 0, 1, 1, 0, 1, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1)\n",
      "This is the decrypted codeword:\n",
      "(1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 1, 0, 1, 1, 0, 1, 0, 0, 0, 1, 0, 1, 1, 1, 1, 0, 1, 1, 1, 0, 1, 1, 0, 1, 0, 1, 1, 0, 1, 0, 0, 0, 1, 0, 1, 1, 1, 0, 0, 0, 1, 0, 0, 1, 0, 1, 0, 1, 1, 0, 0, 1, 0, 1, 0, 1, 1, 1, 0, 1, 1, 1, 0, 1, 1, 0, 0, 1, 1, 0, 1, 0, 0, 1, 0, 0, 0, 1, 0, 0, 0, 1)\n"
     ]
    }
   ],
   "source": [
    "# McEliece is initialised with values m,t. A message must have exactly 2^m-m*t bits, and the codeword will have 2^m bits.\n",
    "\n",
    "# generate random msg with 2^m-m*t bits\n",
    "msg = [0]*(2^m-m*t)\n",
    "for i in range(2^m-m*t):\n",
    "    r = random()\n",
    "    if r>0.5:\n",
    "        msg[i] = 1\n",
    "print(\"This is the message\")\n",
    "print(msg)\n",
    "cryptosystem = McEliece()\n",
    "c = cryptosystem.encrypt(msg)\n",
    "print(\"This is the codeword:\")\n",
    "print(c)\n",
    "print(\"This is the decrypted codeword:\")\n",
    "print(cryptosystem.decrypt(c))\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "509844be-fa80-44b4-8af7-c75d63c05d67",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.4",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.12"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
